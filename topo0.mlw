theory Graph

  use export int.Int
  use set.Fset as S
  use map.Map as M

  (* the graph is defined by a set of vertices and a set of edges *)
  type vertex
  type graph

  function vertices graph: S.set vertex

  function edges graph: S.set (vertex , vertex)

  axiom edges_use_vertices:
  forall g:graph. forall x y:vertex.
    S.mem (x,y) (edges g) -> (S.mem x (vertices g) /\ S.mem y (vertices g))

  (** direct predecessors *)
  function preds graph vertex: S.set vertex

  axiom preds_def:  forall g:graph. forall v:vertex. forall u:vertex.
   S.mem (u,v) (edges g) <-> S.mem u (preds g v)

  (** direct successors *)
  function succs graph vertex: S.set vertex
  axiom succs_def:  forall g:graph. forall v:vertex. forall u:vertex.
   S.mem (u,v) (edges g) <-> S.mem v (succs g u)

  type tsort = M.map vertex int

  predicate sort (g: graph) (tsort:tsort) =
    (* Result in interval [0;S.cardinal (vertices g)[ *)
    (forall x:vertex.
      S.mem x (vertices g) -> 0 <= M.get tsort x < S.cardinal (vertices g))
    /\
    (* Edges are well ordered *)
    (forall x y:vertex.
      S.mem (x, y) (edges g) -> M.get tsort x < M.get tsort y)
    /\
    (* Injectivity *)
    (forall x y:vertex.
      S.mem x (vertices g) -> S.mem y (vertices g) -> M.get tsort x = M.get tsort y -> x = y)


  type order = M.map int vertex

  predicate topological_sort (g: graph) (order: order) (tsort: tsort) =
    (* order and tsort are in bijection *)
    (* ... order (tsort v) = v *)
    (forall v:vertex.
      S.mem v (vertices g) -> M.get order (M.get tsort v) = v)
    /\
    (* ... tsort (order v) = v *)
    (forall i:int.
      0 <= i < (S.cardinal (vertices g)) -> M.get tsort (M.get order i) = i)
    /\
    (* tsort is a good sorting *)
    sort g tsort
    /\
    (* corollary hard to prove: ordered elements are vertices of the graph  *)
    (forall i:int.
      0 <= i < S.cardinal (vertices g) -> S.mem (M.get order i) (vertices g))

  lemma topological_sort_corrolaries:
      forall g. forall order. forall tsort.
       topological_sort g order tsort ->
        (* no backward edges *)
        (forall i, j: int.
        0 <= i <= j < S.cardinal (vertices g) ->
        not (S.mem (M.get order j, M.get order i) (edges g)))

end

theory Path
  use import Graph
  use import list.List
  use import list.Length
  use set.Fset as S

  type path = list vertex

  predicate path_to  (g : graph) (p : path) (to_: vertex) =
    match p with
    | Nil -> false
    | Cons a l ->
      match l with
      | Nil -> S.mem (a,to_) (edges g)
      | Cons b _ -> S.mem (a,b) (edges g) /\ path_to g l to_
      end
    end

  predicate cycle_in (g: graph) =
    exists p. exists v. path_to g (Cons v p) v

end

module TestDefinition
  use import Graph
  use import Path
  use import list.List
  use import option.Option
  use import list.HdTl

  lemma no_empty_path:
    forall g. forall v.
      S.mem v (vertices g) -> not (path_to g Nil v)

  lemma path_for_two_edges:
    forall g. forall x y z.
      (S.mem (x, y) (edges g)) /\ (S.mem (y, z) (edges g)) -> path_to g (Cons x (Cons y Nil)) z

  lemma pred_smaller:
    forall g. forall order. forall tsort.
       topological_sort g order tsort ->
         forall x y:vertex. S.mem (x, y) (edges g) -> M.get tsort x < M.get tsort y

  lemma no_direct_cycle1:
    forall g. forall order. forall tsort.
       topological_sort g order tsort ->
         forall x y:vertex. S.mem (x, y) (edges g) -> not (S.mem (y, x) (edges g))

  lemma no_direct_cycle2:
    forall g. forall order. forall tsort.
       topological_sort g order tsort ->
         forall x:vertex. not (S.mem (x, x) (edges g))

  let rec lemma path_ordered (g:graph) (tsort:tsort) (order:order) (p:path) (v1 v2:vertex)
   requires { topological_sort g order tsort }
   requires { hd p = Some(v1) }
   requires { path_to g p v2 }
   ensures  { M.get tsort v1 < M.get tsort v2 }
   variant { p }
   =
     match p with
     |Cons v1' p' -> path_ordered g tsort order p' v1' v2
     |Nil -> () end

 lemma no_cycle:
    forall g. forall order. forall tsort.
       topological_sort g order tsort -> not (cycle_in g)

end

module Hashtbl

  use import map.Map
  use import set.Fset as S

  type key

  type t 'a model { mutable contents: map key 'a;
                    mutable defined: set key;
                  }

  function ([]) (h: t 'a) (k: key) : 'a = Map.([]) h.contents k

  val create () : t 'a ensures { result.defined = S.empty }

  val clear (h: t 'a) : unit writes {h} ensures { h.defined = S.empty }

  val add (h: t 'a) (k: key) (v: 'a) : unit writes {h}
    ensures { h.contents = (old h.contents[k<-v]) }
    ensures { h.defined = S.add k (old h.defined) }

  val mem (h: t 'a) (k: key) : bool
    ensures { result = (S.mem k h.defined) }

  val find (h: t 'a) (k: key) : 'a
    requires { S.mem k h.defined }
    ensures  { result = h[k] }

  val remove (h: t 'a) (k: key) : unit writes {h}
    ensures { h.defined = S.remove k (old h.defined) }

  val size (h: t 'a) : int
    ensures { result = S.cardinal h.defined }

end

(** Topological sorting by depth-first search using preds *)
module Topo

  use import ref.Ref
  use import Graph
  use import Path
  use import list.List
  use import list.Length
  use set.Fset as S
  use map.Map as M
  clone import Hashtbl as H with type key = vertex
  use map.Const

  type marked = (S.set vertex)

  exception Cycle_found

  predicate inv (g:graph) (m:H.t int) =
    (S.subset m.defined (vertices g))
    /\
    (forall v:vertex.
      S.mem v m.defined -> 0 <= M.get m.contents v < S.cardinal (vertices g))
    /\
    (forall u v:vertex.
      S.mem u m.defined /\ S.mem v m.defined /\ M.get m.contents u = M.get m.contents v
      -> u = v)
    /\
    (forall v:vertex.
      S.mem v m.defined ->
        forall u:vertex.
          S.mem u (preds g v) -> S.mem u m.defined /\ M.get m.contents u < M.get m.contents v)


  let update_seen_path (g:graph) (seen:marked) (old_seen_path:M.map vertex path) (v:vertex) (u:vertex): M.map vertex path
    requires { S.mem u (vertices g) }
    requires { S.mem v (vertices g) }
    requires { S.subset (preds g v) (vertices g) }
    requires { S.subset seen (vertices g) }
    requires { S.mem u (preds g v) }

    requires { not (S.mem v seen) }
    requires { M.get old_seen_path v = Nil }
    requires { forall a:vertex. S.mem a seen -> path_to g (Cons v (M.get old_seen_path a)) a }
    requires { forall a:vertex. not (S.mem a seen) -> M.get old_seen_path a = Nil }

    ensures { forall a:vertex. S.mem a seen -> path_to g (Cons u (M.get result a)) a }
    ensures { path_to g (Cons u (M.get result v)) v }
    ensures { forall a:vertex. not (S.mem a seen) -> M.get result a = Nil }
  =
    let tmp = ref seen in
(*    let out = ref (Const.const Nil) in*)
    let out = ref old_seen_path in
    while not (S.is_empty !tmp) do
      invariant { forall a:vertex. S.mem a (S.diff seen !tmp) -> path_to g (Cons u (M.get !out a)) a }
      invariant { forall a:vertex. S.mem a !tmp -> a <> v }
      invariant { M.get !out v = Nil }
      invariant { S.subset !tmp seen }
      invariant { forall a:vertex. not (S.mem a seen) -> M.get !out a = Nil }
      variant { S.cardinal !tmp }
      let a = S.choose !tmp in
      out := M.set !out a (Cons v (M.get old_seen_path a));
      tmp := S.remove a !tmp
    done;
    !out

  let rec dfs (g:graph) (v:vertex)
              (seen:marked) (tsort:H.t int)
              (seen_path:M.map vertex path)
              : unit
    requires { inv g tsort }
    (* v est dans le graphe *)
    requires { S.mem v (vertices g) }
    (* Les vertices vus sont dans le graphe *)
    requires { S.subset seen (vertices g) }
    requires { S.subset (preds g v) (vertices g) }
    (* 3.5 *)
    requires { forall v:vertex. S.mem v seen /\ S.mem v (vertices g) -> not (S.mem v tsort.defined) }
    (* 3.6 *)
    requires { forall u:vertex. S.mem u (vertices g) /\ S.mem u tsort.defined -> M.get tsort.contents u < S.cardinal tsort.defined }
    (* 3.8 *)
    requires { forall a:vertex. S.mem a seen -> path_to g (Cons v (M.get seen_path a)) a }
    requires { forall a:vertex. not (S.mem a seen) -> M.get seen_path a = Nil }

    variant { (S.cardinal (vertices g)) - (S.cardinal seen) }

    ensures { inv g tsort }
    ensures { S.subset seen (vertices g) }
    ensures { S.subset (preds g v) (vertices g) }
    ensures { S.mem v tsort.defined }
    ensures { S.subset (old tsort.defined) tsort.defined }
    (* 3.5 *)
    ensures { forall v:vertex. S.mem v seen /\ S.mem v (vertices g) -> not (S.mem v tsort.defined) }
    (* 3.6 *)
    ensures { not S.mem v (old tsort.defined) -> M.get tsort.contents v = S.cardinal tsort.defined - 1 }
    ensures { forall u:vertex. S.mem u (vertices g) /\ S.mem u tsort.defined -> M.get tsort.contents u < S.cardinal tsort.defined }
    (* 3.7 *)
    ensures { not (S.mem v (old tsort.defined)) -> S.subset (preds g v) tsort.defined }
    raises  { Cycle_found -> cycle_in g  }
   =
  'Init:
   if S.mem v seen then raise Cycle_found;
   if not (H.mem tsort v) then begin
  'Init_loop:
    begin
     let p = ref (preds g v) in
     let seen' = S.add v seen in
     while not (S.is_empty !p) do
      invariant { inv g tsort }
      invariant { S.subset !p (vertices g) }
      invariant { S.subset !p (preds g v) }
      invariant { S.subset (S.diff (preds g v) !p) tsort.defined }
      invariant { S.subset (at tsort.defined 'Init_loop) tsort.defined }
      (* 3.5 *)
      invariant { forall v:vertex. S.mem v seen' /\ S.mem v (vertices g) -> not (S.mem v tsort.defined) }
      (* 3.6 *)
      invariant { forall u:vertex. S.mem u (vertices g) /\ S.mem u tsort.defined -> M.get tsort.contents u < S.cardinal tsort.defined }
      (* 3.7 *)
      invariant { forall u:vertex. S.mem u (vertices g) /\ S.mem u (S.diff (preds g v) !p) -> S.mem u tsort.defined }
      (* 3.8 *)
      invariant { forall a:vertex. S.mem a seen -> path_to g (Cons v (M.get seen_path a)) a }
      variant { S.cardinal !p }

      let u = S.choose !p in
      dfs g u seen' tsort (update_seen_path g seen seen_path v u);
      p := S.remove u !p
      done;
    end;
     H.add tsort v (H.size tsort)
    end


    let topo_tsort (g:graph): H.t int
      raises  { Cycle_found -> true  }
      ensures { sort g result.contents }
      ensures { S.(==) result.defined (vertices g) }
(*      diverges *)
      =
    'Init:
      let tsort = H.create () in
      let p = ref (vertices g) in
      while not (S.is_empty !p) do
        invariant { inv g tsort }
        invariant { S.subset !p (vertices g) }
        invariant { S.subset (S.diff (vertices g) !p) tsort.defined }
        (* 3.6 *)
        invariant { forall u:vertex. S.mem u (vertices g) /\ S.mem u tsort.defined -> M.get tsort.contents u < S.cardinal tsort.defined }
        variant { S.cardinal !p }
        let u = S.choose !p in
        dfs g u (S.empty) tsort (Const.const Nil);
        p := S.remove u !p
     done;
     tsort

    use import array.Array

    type topo = {
      order: array vertex;
      tsort: H.t int
    }

    let rec init_todo (deb: int) (fin:int): S.set int
    requires { deb <= fin }
    variant { fin - deb }
    ensures { forall i:int. S.mem i result <-> deb <= i < fin }
    ensures { S.cardinal result = fin - deb }
    =
      if deb = fin then
        S.empty
      else
        S.add deb (init_todo (deb+1) fin)


    let topo (g:graph): topo
      raises  { Cycle_found -> true }
      requires { not (S.is_empty (vertices g)) }
      ensures { topological_sort g result.order.elts result.tsort.contents }
      ensures { S.(==) result.tsort.defined (vertices g) }
      ensures { result.order.length = S.cardinal (vertices g) }
      diverges
     =
      let tsort = topo_tsort g in
      let order = Array.make (S.cardinal (vertices g)) (S.choose (vertices g)) in
      let p = ref (vertices g) in
      let todo = ref (init_todo 0 (S.cardinal (vertices g))) in
      while not (S.is_empty !p) do
        invariant { S.subset !p tsort.defined }
        (* Invariant pour order (tsort v) = v *)
        invariant {
          forall v:vertex.
            not (S.mem v !p) /\ S.mem v tsort.defined ->
            Array.get order (M.get tsort.contents v) = v
        }
        (* Invariant pour la ghost variable todo *)
        invariant { S.cardinal !p = S.cardinal !todo }
        invariant { forall v:vertex. S.mem v !p -> exists i:int. S.mem i !todo /\ M.get tsort.contents v = i }
        (* Invariant pour tsort (order i) = i *)
        invariant {
          forall i:int.
            not (S.mem i !todo) /\ (0 <= i < (S.cardinal (vertices g))) ->
            M.get tsort.contents (Array.get order i) = i
        }
        invariant {
          forall i:int.
            0 <= i < (S.cardinal (vertices g)) ->
            S.mem (Array.get order i) (vertices g)
        }
        let u = S.choose !p in
        let ind = H.find tsort u in
        order[ind] <- u;
        p := S.remove u !p;
        todo := S.remove ind !todo
     done;
     {order = order; tsort = tsort}

end

theory Dominator
  use import Graph
  use import list.List
  use import list.Length
  use import list.Elements
  use set.Fset as S
  use import Path

  predicate domine (g:graph) (root x y:vertex) =
   y <> root /\
   forall p. path_to g (Cons root p) y -> S.mem x (S.add root (elements p))

  (** immediate dominator *)
  predicate idomine (g:graph) (root x y:vertex) =
   domine g root x y /\ (forall x'. x' <> x -> domine g root x' y -> domine g root x' x)

end

module Dominators

  use import ref.Ref
  use import Graph
  use import Path
  use import Dominator
  use import list.List
  use import list.Length
  use import list.Elements
  use import list.Append
  use set.Fset as S
  use map.Map as M
  use map.Const
  use import array.Array
  use import Topo

  predicate not_disjoint (s1 s2: S.set vertex) =
   not (S.is_empty (S.inter s1 s2))

  lemma not_disjoint_mem:
    forall s1 s2: S.set vertex. not_disjoint s1 s2 <-> (exists x. S.mem x s1 /\ S.mem x s2)

 lemma not_disjoint_singleton:
    forall s1: S.set vertex. forall x: vertex. not_disjoint s1 (S.singleton x) <-> S.mem x s1

  exception Root

  let idoms g (root:vertex) (topo:Topo.topo) (ghost exi_path : M.map vertex path)
     : array int
   requires { S.mem root (vertices g) }
   requires { S.is_empty (preds g root) }
   requires { forall v. v <> root -> S.mem v (vertices g) -> path_to g (Cons root (M.get exi_path v)) v }

   requires { topological_sort g topo.Topo.order.elts topo.Topo.tsort.Topo.H.contents }
   requires { S.(==) topo.Topo.tsort.Topo.H.defined (vertices g) }
   requires { topo.Topo.order.length = S.cardinal (vertices g) }

   ensures { forall i. 0 < i < S.cardinal (vertices g) ->
              idomine g root (topo.Topo.order[result[i]]) (topo.Topo.order[i]) }
   ensures { result[0] = 0 }
   diverges
   =
    let a = Array.make (S.cardinal (vertices g)) (-1) in
    a[0] <- 0;
    for nv=1 to (S.cardinal (vertices g) - 1) do
      'Start_body:
      let v = topo.Topo.order[nv] in
      let rec find_common n1 n2
        raises  { Root -> idomine g root root v }
        diverges
       =
        if n1 = n2 then n1
        else
        let n1, n2 = if n2 < n1 then (n1,n2) else (n2,n1) in
        if a[n1] = 0 then raise Root
        else find_common a[n1] n2
      in
    try
      let p = ref (preds g v) in
      assert { not (S.is_empty !p) };
      let u = S.choose !p in
      (if u = root then raise Root);
      let nu = Topo.H.find topo.Topo.tsort u in
      let idom_v = ref nu in
      p := S.remove u !p;
      while not (S.is_empty !p) do
       let u = S.choose !p in
       (if u = root then raise Root);
       let nu = Topo.H.find topo.Topo.tsort u in
       idom_v := find_common !idom_v nu;
       p := S.remove u !p;
      done;
      a[nv] <- !idom_v;
    with Root ->
      a[nv] <- 0;
    end
    done;
    a

end
